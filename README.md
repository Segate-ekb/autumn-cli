# autumn-cli 

Обертка над [cli](https://github.com/khorevaa/cli) библиотекой, которая предоставляет возможности создания консольных приложений на фреймворке [ОСень](https://github.com/autumn-library/autumn).

## Подключение и пример использования

Точка входа в приложение

```main.os```
```bsl
#Использовать autumn
#Использовать autumn-cli
#Использовать "Каталог/С/Классами/Команд"

Поделка = Новый Поделка();
Поделка
	.ПросканироватьКаталог("Каталог/С/Классами/Команд")
	.ЗапуститьПриложение();
```

Допустим мы хотим сделать приложение, которое имеет следующие команды:

```plus``` - Принимает один числовой аргумент, и прибавляет к нему 10. Выводит результат в консоль.

```hello``` - Имеет опцию ```name```, и выводит в консоль приветствующее сообщение.

```date``` - Имеет две подкоманды, ```day``` выводит в консоль начало дня, и ```month``` выводит в консоль начало месяца.

## Аргументы

Подготовим первую команду:

```Каталог/С/Классами/Команд/КомандаПлюс.os```
```bsl
&Аргумент(Имя = "ARG", Описание = "Значение числа") // Определим аргумент, и дадим ему описание
&ТЧисло // Укажем тип аргумента
Перем ПереданноеЧисло;

&КомандаПриложения(Имя = "p plus", Описание = "Прибавляет 10 к заданному числу") // Аннотация, обозначающая, что этот класс - консольная команда
Процедура ПриСозданииОбъекта()
		
КонецПроцедуры

&ВыполнениеКоманды // Методы, помеченные этой аннотацией будут выполнены при вызове команды.
Процедура ВывестиРезультатСложения() Экспорт

	Результат = ПереданноеЧисло + 10;

	Сообщить("Результат сложения: " + Результат);
	
КонецПроцедуры
```

В этом примере мы определили класс как команду консольного приложения с помощью аннотации 

```&КомандаПриложения(Имя = "p plus", Описание = "Прибавляет 10 к заданному числу")```

В которой указали имя команды и ее описание. А для определения аргумента команды, была создана переменная с аннотацией 

```&Аргумент(Имя = "ARG", Описание = "Значение числа")```

Для того, что бы дать понять библиотеке, какой метод нужно выполнить при вызове команды, нужно сделать экспортный метод с аннотацией

```&ВыполнениеКоманды```

## Опции

Команда с опцией

```Каталог/С/Классами/Команд/КомандаПривет.os```
```bsl
&Опция(Имя = "n name", Описание = "Имя пользователя")
Перем ИмяПользователя;

&КомандаПриложения(Имя = "h hello", Описание = "Поздоровается с пользователем")
Процедура ПриСозданииОбъекта()
		
КонецПроцедуры

&ВыполнениеКоманды
Процедура ПоприветствоватьПользователя() Экспорт

	ТекстСообщения = "Привет " + ИмяПользователя + "!";

	Сообщить(ТекстСообщения);
	
КонецПроцедуры
```

В этом примере, для определения опции команды, была создана переменная с аннотацией 

```&Опция(Имя = "n name", Описание = "Имя пользователя")```

## Подкоманды

Теперь определим последнюю команду, которая имеет две подкоманды

```Каталог/С/Классами/Команд/КомандаДата.os```
```bsl
&КомандаПриложения(Имя = "d date", 
					Описание = "Выводит дату",
					Подкоманда = "day",
					Подкоманда = "month"
)
Процедура ПриСозданииОбъекта()
		
КонецПроцедуры
```

в аннотации ```&КомандаПриложения``` которой, с помощью параметров ```Подкоманда``` мы указали имена подчиненных команд.

Осталось определить эти две подкоманды

```Каталог/С/Классами/Команд/ПодкомандаДень.os```
```bsl
&КомандаПриложения(Имя = "day", Описание = "Выводит дату - начало дня", ЭтоПодкоманда = Истина)
Процедура ПриСозданииОбъекта()
		
КонецПроцедуры

&ВыполнениеКоманды
Процедура СообщитьДень() Экспорт

	Сообщить(НачалоДня(ТекущаяДата()));

КонецПроцедуры
```

```Каталог/С/Классами/Команд/ПодкомандаМесяц.os```
```bsl
&КомандаПриложения(Имя = "month", Описание = "Выводит дату - начало месяца", ЭтоПодкоманда = Истина)
Процедура ПриСозданииОбъекта()
		
КонецПроцедуры

&ВыполнениеКоманды
Процедура СообщитьМесяц() Экспорт

	Сообщить(НачалоМесяца(ТекущаяДата()));
	
КонецПроцедуры
```

Стоит отметить, что тут в аннотации ```&КомандаПриложения```, устанавливается параметр ```ЭтоПодкоманда = Истина```

## Типы аргументов и опций

Имеется набор аннотаций, которые можно добавить к полям команд, которые обозначат, к какому типу нужно приводить эти поля

```bsl
&ВОкружении
&Флаговый
&Флаг
&СкрытьВСправке
&ПоУмолчанию
&ТБулево
&ТДата
&ТЧисло
&ТСтрока
&ТМассивДат
&ТМассивЧисел
&ТМассивСтрок
&Псевдоним
&Описание
&Обязательный
&ПодробноеОписание
```

## Настройка версии и имени приложения.

Настройку вывода версии и имени приложения можно осуществить двумя способами.

1) Создать рядом с точкой входа файл 

```autumn-properties.json```
```json
{
    "cli": {
        "ИмяПриложения": "cli_test",
        "ПолноеИмяПриложения": "cli_test v%{cli.ВерсияПриложения}",
        "ВерсияПриложения": "1.0.1"
    }
}
```

2) В каталог с командами, которые будут загружены в контекст осени, добавить желудь с определенным интерфейсом

```Каталог/С/Классами/Команд/ПодкомандаМесяц.os```
```bsl
&Желудь("ОписаниеКонсольногоПриложения")
&Верховный
Процедура ПриСозданииОбъекта()
КонецПроцедуры

Функция ИмяПриложения() Экспорт
	Возврат "demo-cli";
КонецФункции

Функция ПолноеИмяПриложения() Экспорт
	Возврат "Демонстрационное консольное приложение";
КонецФункции

Функция ВерсияПриложения() Экспорт
	Возврат "1.0";
КонецФункции
```

## Больше примеров

Описанные классы можно посмотреть в каталоге [example](example/) и еще больше примеров команд в каталоге [tests](tests/Классы)


